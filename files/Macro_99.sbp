'!FABMO!name:Set Home Position
'!FABMO!description:Update Offsets from Limit Switches Used During Homing Routine.
'!FABMO!enabled:true
SU,0                   ' Set to Inches`

&x_start_pos = %(1)
&y_start_pos = %(2)

GOSUB initialize_variables
GOSUB clear_z
GOSUB calc_y_offset
GOSUB calc_x_offset
GOSUB go_to_offset

END

clear_z: 
    PZ, %(3) +  &searchDist_Z, &searchSpeed_Z, &proxNum_Z  ' Search for prox switch
    JZ, %(3) - &homePullBack
    RETURN
    
calc_y_offset:
	PY, %(2) - &searchDist_Y, &searchSpeed_XY, &proxNum_Y   ' Search for prox switch
    JY, %(2) + &homePullBack
    &target_ck =  (-1 * (&homeOff_Y + &ooch))
    PY, %(2) + &target_ck, &slowSearchSpeed_XY, &proxNum_Y  ' Slowly search for prox in accurate check
    IF %(2) = &target_ck THEN GOTO fail_missed_target
    &new_y_pos = %(2)
    &y_offset = &y_start_pos - &new_y_pos
    JY, %(2) + &homeOff_Y
    RETURN 
    
calc_x_offset:
	PX, %(1) - &searchDist_X, &searchSpeed_XY, &proxNum_X  ' Search for prox switch 
    JX, %(1) + &homePullBack
    &target_ck = (-1 * (0.5 + $x_backoff)) 
    PX, %(1) + &target_ck, &slowSearchSpeed_XY, &proxNum_X  ' Slowly search for prox in accurate check
    IF %(1) = &target_ck THEN GOSUB fail_missed_target
    &new_x_pos = %(1)
    &x_offset = &x_start_pos - &new_x_pos
    JX, %(1) + &homeOff_X
    RETURN
    
go_to_offset:
	J2, &x_start_pos, &y_start_pos
    $sb_homeOff_X  = &x_offset
    $sb_homeOff_Y = &y_offset
    VA, 0,0
    RETURN 
    
  fail_missed_target:
      PAUSE "Target Not Triggered!"      ' Did not hit target.
      END
      
      '--- Initialize Variables and Adjust Standard Macro Variables for Current Tool and Conditions as Needed
initialize_variables:
    &proxNum_Z = $sb_proxNum_Z
    &check_prox_Z = $sb_proxCk_Z							  
    &proxNum_X = $sb_proxNum_X
    &check_prox_X = $sb_proxCk_X							  
    &proxNum_Y = $sb_proxNum_Y
    &check_prox_Y = $sb_proxCk_Y							  

  'Adjust to current UNITS of tool (handles whether tool Macro Variables are for INCHES or MM)
	&dist_mult = 1.0
	&Zoff_mult = 1.0
	IF %(25) == 1 THEN GOTO tool_MM							  ' Read UNIT of Tool; System Variable #25
	  tool_IN:
	    $sb_current_cutter_Zoffset := .25                     ' Initialize variable in IN for case tool cutter not zeroed
		$sb_current_cutter_Zoffset_Units := 0
		IF $sb_current_cutter_Zoffset_Units == "IN" THEN GOTO ok_IN
		  &Zoff_mult = 0.0393701
		ok_IN:
	    IF $sb_varTableUNITS == "IN" THEN GOTO continue_with_variables
		&dist_mult = 0.0393701                                ' Defined in MM: Multiplier to redefine distances from mm to inches 
	    GOTO continue_with_variables
	  tool_MM:
	    $sb_current_cutter_Zoffset := 6.35                    ' Initialize variable in MM for case tool cutter not zeroed
		$sb_current_cutter_Zoffset_Units := 1
		IF $sb_current_cutter_Zoffset_Units == "MM" THEN GOTO ok_MM
		  &Zoff_mult = 25.4
		ok_MM:
	    IF $sb_varTableUNITS == "MM" THEN GOTO continue_with_variables
		&dist_mult = 25.4                                     ' Defined in IN: Multiplier to redefine distances from inches to mm 
	    GOTO continue_with_variables
  'continue adjusting units for current working variables ...
	continue_with_variables:
		&target_ck = 0
		&ooch = $sb_ooch * &dist_mult
		&backOff_dist = &ooch * 5
 		&searchDist_Z = $sb_searchDist_Z * &dist_mult
		&searchSpeed_Z = $sb_searchSpeed_Z * &dist_mult
		&slowSearchSpeed_Z = $sb_slowSearchSpeed_Z * &dist_mult
		&searchDist_X = $sb_searchDist_X * &dist_mult
		&searchDist_Y = $sb_searchDist_Y * &dist_mult
		&searchSpeed_XY = $sb_searchSpeed_XY * &dist_mult
		&slowSearchSpeed_XY = $sb_slowSearchSpeed_XY * &dist_mult
		&homePullBack = $sb_homePullBack * &dist_mult
		&homeOff_Z = $sb_homeOff_Z * &dist_mult
		&homeOff_X = $sb_homeOff_X * &dist_mult
		&homeOff_Y = $sb_homeOff_Y * &dist_mult
	RETURN
